#if {!@file_exists{.tt/3k/vars/elemental.var}}
{
	#class {elemental_vars} {open};
	#var elementalv[cycle][type] {default};

#nop When down to this many corpses, switch to gather cycle type;
	#var elementalv[cycle][min_corpses] {5};

#nop When up to this many corpses, switch to spend cycle type, set to 0 to use coffin max;
	#var elementalv[cycle][max_corpses] {0};
	#class elemental_vars write {.tt/3k/vars/elemental.var};
	#class {elemental_vars} {close};
}
{
  #class elemental_vars read {.tt/3k/vars/elemental.var};
};

#class {elemental} {open}

#send {!vlist};
#send {!escore2};
#send {!reflex CLEAR\n!reflex add hp 99% evoke rebuild\n!reflex add sp 85% evoke mental depletion\n!reflex add sp 6% evoke natural convergence\n!reflex add sp 80% evoke soul wave\n!reflex add gp1 60% evoke energy drain};

/* Set Guild Variable*/
#var guild elemental

#var elemental[current_cycletype] {$elementalv[cycle][type]};

#nop TODO: This variable needs to be set by something else;
#var elemental[coffin][max] {25};

#var elemental[nc] {2};
#var elemental[incombat] {0};
#var elemental[md] {0};
#var elemental[age] {0};
#var elemental[eval] {0};
#var elemental[atomic] {0};
#var elemental[atomicgem] {0};
#var elemental[atomicnc] {0};

#function {guild_halt} {
#nop No guild pauses implemented;
	#return  0;
};

#function {guild_cycletype} {
	#return $elemental[current_cycletype];
};

#alias {ecycleset} {
	#if {"%1"!="default" && "%1"!="gather" && "%1"!="spend" && "%1"!="aegis"} {
		#showme Usage: ecycleset [default|gather|spend|aegis];
		#showme        If using "default", it will stay in default untill you set it to something else.;
		#showme        If using "gather" or "spend", it will toggle between the two based on the values;
		#showme        in elementalv[cycle][min_corpses] and elementalv[cycle][max_corpses].;
		#showme        Your current cycle is: @guild_cycletype{};
	} {
		#var elemental[current_cycletype] {%1};
		#var elementalv[cycle][type] {%1};
save_guild_vars;
	}
};

#alias {ecyclecheck} {
	#nop If we're in default, stay in default. Only swap between gather and spend.;
	#if {"$elemental[current_cycletype]"!="default"} {
		#if {"$elemental[current_cycletype]"=="gather"} {
			#if {!$elementalv[cycle][max_corpses]} {#var _max_corpses {$elemental[coffin][max]}} {#var _max_corpses {$elementalv[cycle][max_corpses]}};
			#if {$corpses[coffin] >= $_max_corpses} {
				#var elemental[current_cycletype] {spend};
				update_chat <138>Auto switching to spend cycle.<099>;
			};
		} {
			#if {$corpses[coffin] < $elementalv[cycle][min_corpses]} {
				#var elemental[current_cycletype] {gather};
				update_chat <138>Auto switching to gather cycle.<099>;
			};
		};
	};
};

#alias {save_guild_vars} {#class elemental_vars write {.tt/3k/vars/elemental.var}};


/* HP Bars */
#act {^ HP:%d/%d C:%d\%  NRG:%d/%d(%d)  SP:%d/%d  S:%d(%d\%) G2N:%d%w  T:{[\w\s]+}:%d\%} {
	#var my[hp][current]		{%1};
	#var my[hp][max]		{%2};
	#var my[cohesion]		{%3};
	#var my[gp1][current]		{%4};
	#var my[gp1][max]		{%5};
	#var my[emit][level]		{%6};
	#var my[sp][current]		{%7};
	#var my[sp][max]		{%8};
	#var my[blasts]			{%9};
	#var my[reset]			{%10};
	#var my[g2n]			{%11};
	#var g2n_mod			{%12};
	#var enemy[name]		{%13};
	#var enemy[hp]			{%14}
}

/* Update Status Alias */
#alias {update_status} {
	#if {"$enemy[hp]" == ""} {#var elemental[incombat] {0}} {#var elemental[incombat] {1}};
	ecyclecheck;
	#if {"$cooler" == "on" || $corpses[cooler] > 0} {
		#format {sb1_cooler} {%+2s} {$corpses[cooler]};
		#var {sb1_cooler} { CL:<130>$sb1_cooler<088>}
	} {
		#var {sb1_cooler} {}
	};
	#if {"$freezer" == "on" || $corpses[freezer] > 0} {
		#format {sb2_freezer} {%+2s} {$corpses[freezer]};
		#var {sb2_freezer} { FZ:<130>$sb2_freezer<088>}
	} {
		#var {sb2_freezer} {}
	};
	#var {my[hp][graph]}		{@graph{$my[hp][current];$my[hp][max]}};
	#var {my[sp][graph]}		{@graph{$my[sp][current];$my[sp][max]}};
	#var {my[gp1][graph]}		{@graph{$my[gp1][current];$my[gp1][max]}};
	#var {enemy[graph]}		{@graph{$enemy[hp];100}};
	#format {hp}			{%+5s} {@colscale{$my[hp][current];$my[hp][max]}};
	#format {hp_max}		{%+5s} {<128>$my[hp][max]<088>};
	#format {sp}			{%+4s} {@colscale{$my[sp][current];$my[sp][max]}};
	#format {sp_max}		{%+4s} {<128>$my[sp][max]<088>};
	#format {nrg}			{%+4s} {@colscale{$my[gp1][current];$my[gp1][max]}};
	#format {nrg_max}		{%+4s} {<128>$my[gp1][max]<088>};
	#format {ehealth}		{%+3s} {@colscale{$enemy[hp];100}};
	#format {corpses[inventory]}    {%+2s} {$corpses[inventory]};
	#format {corpses[coffin]}       {%+2s} {$corpses[coffin]};
	#if {"$draw[hpbar]" == "on"} {
		#showme {H:${hp}/${hp_max}  S:${sp}/${sp_max}  N:${nrg}/${nrg_max}  E:   ${ehealth}%    IC:<130>${corpses[inventory]}<088>${sb1_cooler}  Cycle:<130>@guild_cycletype{}<088>  NC:<130>$elemental[nc]<088>  MD:<130>$elemental[md]<088>} {-4};
		#showme {H:${my[hp][graph]}   S:${my[sp][graph]} N:${my[gp1][graph]} E:${enemy[graph]} CF:<130>${corpses[coffin]} <088>${sb2_freezer}} {-3}
	};
	#if {"$draw[gline]" == "on"} {
		#showme {$mip[gline][1]  $mip[gline][2] <128>R:<088>$mip[round]} {-6}
	};

	#if {$elemental[atomic] == 0 && $my[reset] > 94 && $enemy[hp] > 20 && $my[blasts] > 0 && "$mobname" != ""} {#var elemental[atomic] {1};ff $mobname;#delay {4} {#var elemental[atomic] {0}}};
	#if {$elemental[atomic] == 0 && "$my[equip]" == "off" && $bot && "$bots[stepper]" == "zelligars"} {#var elemental[atomic] {1};#send {!evoke equipollent};#delay {4} {#var elemental[atomic] {0}}};
	#if {$elemental[atomicnc] == 0 && $elemental[nc] > 0 && $my[reset] > 98 && $my[sp][current] < 4100} {#var elemental[atomicnc] {1};#send {!evoke natural convergence};#delay {10} {#var elemental[atomicnc] {0}}};
	#if {$elemental[atomicgem] == 0 && $cooldown[eternalheal] && $cooldown[hmheal] && $my[sp][current] < 100} {#var elemental[atomicgem] {1};#send {!get gem from vortex\n!release gem\n!put gem in vortex};#delay {4} {#var elemental[atomicgem] {0}}};
}

/* Corpse Trigger */
#alias {corpsetrig} {
	#send {!xp};
	#var elemental[md] {0};
	#var elemental[age] {0};
	#var elemental[eval] {0};
};
#alias {k} {#send {!kill %0};#var mobname {%0}};
#alias {eval} {#send evoke evaluate};
#alias {gs} {escore};
#alias {gs2} {escore2};
#alias {gwho} {ewho};
#alias {ff} {
	#loop 1 $my[blasts] cnt {
		#send {!evoke time stone};
		#send {!activate stone};
		#send {!despam}
	};
	#loop 1 $my[blasts] cnt {
		#send {!give time stone to %0}
	};
	#loop 1 $my[blasts] cnt {
		#send {!drop time stone}
	};
};
#alias profile_nobalangool {
dscaler aegis1 95;
dscaler aegis2 95;
dscaler aegis3 95;
dscaler aegis4 95;
dscaler aegis5 95;
dscaler chess 150;
dscaler mahjongg 99;
dscaler sectionz 91;
dscaler smithy 91;
dscaler trans 117;
dscaler treehouse 110;
dscaler zelligars 150;
dscaler rim2 91;
};
#alias prof25m {
dscaler chess 150;
dscaler mahjongg 100;
dscaler sectionz 115;
dscaler smithy 91;
dscaler trans 117;
dscaler treehouse 122;
dscaler zelligars 150;
dscaler rim2 91;
};
#alias prof30m {
dscaler aegis1 110;
dscaler aegis2 110;
dscaler aegis3 110;
dscaler aegis4 110;
dscaler aegis5 110;
dscaler chess 150;
dscaler mahjongg 110;
dscaler sectionz 117;
dscaler smithy 91;
dscaler trans 135;
dscaler treehouse 129;
dscaler zelligars 150;
dscaler rim2 91;
};
#alias prof45m {
dscaler aegis1 119;
dscaler aegis2 119;
dscaler aegis3 119;
dscaler aegis4 119;
dscaler aegis5 119;
dscaler chess 150;
dscaler mahjongg 115;
dscaler sectionz 125;
dscaler smithy 91;
dscaler trans 150;
dscaler treehouse 129;
dscaler zelligars 150;
dscaler rim2 91;
};
#alias {el} {chat time};
#alias {ju} {chat el};
#alias {res} {#send {!evoke resolve with %0};#send {!evoke prismatic emission with %0}};

#act {^Sorry, they have no excess energy to drain.} {update_chat <118>!!! Mob cannot be energy drained !!!<099>};
#act {^You focus inward, reaching back through the Astral plane to your home} {#var elemental[md] {1}};
#act {^You combine a %*-%* soul gem with a %*-striped jade soul gem.} {#send {!put gem in vortex};#send {!put gem in disc}};
#act {^You combine a %*-striped jade soul gem with a %*-%* soul gem.} {#send {!put gem in vortex};#send {!put gem in disc}};
#act {^You focus inward, searching deep within yourself for the true link to} {#math {elemental[nc]} {$elemental[nc] - 1}};
#act {^Your gems have reset} {#var elemental[nc] {2}};
#act {^Your time stones have reset} {#var elemental[nc] {2}};
#act {^As the last bit of life force escapes your enemy you quickly capture its} {#send !combine};
#act {^-- You may draw upon the forces of nature %d time} {#format elemental[nc] {%1} {%1}};
#act {^You may not draw upon nature's power at this time.} {#send !hmheal};
#act {^You need rest to use that power again.} {#send !hk};
#NOP #act {{evaluate the strengths and weaknesses of (.*)}} {#if {"%2" == "Lrille, Killergate's Golden"} {#NOP} {#act {{-\> This \w seems to be most susceptible to (\w) attacks\.}} {#send !evoke resolve with %%2;#send !evoke prismatic emission with %%2;#unact {{-\> This \w seems to be most susceptible to (\w) attacks\.}}}}};

#gag {^You may not draw upon nature's power at this time.};
#gag {^That mind cannot be depleted again, by you.};
#gag {^You are already tapped into your home plane.};
#gag {^Not so fast!};
#gag {^You lack the inner strength to call forth another link.};
#gag {^You need rest to use that power again.}
#gag {^Evoke on who/what?};
#gag {^You reach through the vortex to your elemental plane and solidify your form};
#gag {^as much as possible.};
#gag {^Your form shimmers and becomes impenetrable.};
#gag {^You have become grounded in this plane yet again.};
#gag {^A bolt of pure energy flies out of the sky striking %*!};
#gag {^Your great speed allows you to attack again!};

#class {elemental} {close}
